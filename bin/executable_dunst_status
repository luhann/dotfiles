#!/usr/bin/env fish

# Check if dunstctl is available
if not command -v dunstctl >/dev/null
    echo "Error: dunstctl not available" >&2
    exit 1
end

# Get dunst pause status
set -l is_paused (dunstctl is-paused)
set -l waiting_count (dunstctl count waiting)
set -l num_notifications (dunstctl history | jq '.data[0] | length')

# Determine output format based on session type
if test "$XDG_SESSION_TYPE" = "wayland"
    # Waybar format (JSON)
    set -l enabled_icon " "
    set -l disabled_icon " "
    
    if test "$is_paused" = "true"
        # Notifications are paused
        set output_text "$disabled_icon"
        if test "$waiting_count" -gt 0
            set output_text "$disabled_icon $waiting_count"
        end
        echo '{"text": "'"$output_text"'", "class": "paused", "tooltip": "'"$num_notifications"'"}'
    else
        # Notifications are active
        echo '{"text": "'"$enabled_icon"'", "alt": "active", "tooltip": "'"$num_notifications"'"}'
    end
else
    # Polybar format (plain text with formatting)
    if test "$is_paused" = "true"
        echo "%{F#821717} %{F-}"
    else
        echo ""
    end
end
